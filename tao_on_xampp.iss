; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define BasePath "C:\Users\John\Documents\ISS_test"
#define TaoFolder "xampp"
#define MyAppVersion "3.1.0"
#define MyAppName "TAO under XAMPP"
#define MyAppPublisher "Open Assessment Technologies (OAT) S.A."
#define MyAppURL "http://www.taotesting.com/"
#define MyAppExeName "xampp-control.exe"
#define group "TAO Tools"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{530BC45D-D77C-4489-B2ED-36D217E6F76F}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={sd}\xampp
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
LicenseFile={#BasePath}\{#TaoFolder}\htdocs\gpl-2.0.txt
OutputDir=C:\Users\John\Documents\ISS_test\output
OutputBaseFilename=TAO_{#MyAppVersion}_Windows_setup
;SetupIconFile=C:\Users\John\Documents\ISS_test\xampp\htdocs\favicon.ico
SetupIconFile=C:\Users\John\Documents\ISS_test\tao-logo.ico
Compression=lzma2/ultra
SolidCompression=yes


[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "{#BasePath}\{#TaoFolder}\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Run]
Filename: "{app}\setup_xampp.bat"; WorkingDir: "{app}"; 
Filename: "{app}\xampp_start.exe"; WorkingDir: "{app}"; Flags: waituntilidle runhidden;
Filename: "{app}\oat\setup_tao.bat"; WorkingDir: "{app}"; Flags: hidewizard;

[UninstallRun]
Filename: "{app}\xampp_stop.exe"; WorkingDir: "{app}"; Flags: waituntilidle runhidden;

[Tasks] 
Name: "starticon"; Description: "Create a &start menu icons"; GroupDescription: "Additional icons:"
Name: "desktopicon"; Description: "Create a &desktop menu icons"; GroupDescription: "Additional icons:" 
 

[Icons] 
Name: "{group}\Start TAO"; Filename: "{app}\xampp_start.exe"; Tasks: starticon; IconFilename: "{app}\oat\start-tao.ico"
Name: "{group}\Stop TAO"; Filename: "{app}\xampp_stop.exe"; Tasks: starticon; IconFilename: "{app}\oat\stop-tao.ico" 
Name: "{group}\TAO Login"; Filename: "http://localhost:89/"; Tasks: starticon; 
Name: "{commondesktop}\TAO Login"; Filename: "http://localhost:89/"; Tasks: desktopicon;
Name: "{commondesktop}\Start TAO"; Filename: "{app}\xampp_start.exe"; Tasks: desktopicon; IconFilename: "{app}\oat\start-tao.ico"
Name: "{commondesktop}\Stop TAO"; Filename: "{app}\xampp_stop.exe"; Tasks: desktopicon; IconFilename: "{app}\oat\stop-tao.ico"


[code]

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  Result := True;
  if CurPageID = wpSelectDir then begin
    if ((Pos(' ',WizardForm.DirEdit.Text) > 0) 
      or (Pos('/',WizardForm.DirEdit.Text) > 0)
      or (Pos('-',WizardForm.DirEdit.Text) > 0)
      or (Pos('.',WizardForm.DirEdit.Text) > 0)
      or (Pos('[',WizardForm.DirEdit.Text) > 0)
      or (Pos(']',WizardForm.DirEdit.Text) > 0)
      or (Pos('!',WizardForm.DirEdit.Text) > 0)
      or (Pos('{',WizardForm.DirEdit.Text) > 0)
      or (Pos('}',WizardForm.DirEdit.Text) > 0)
      or (Pos('''',WizardForm.DirEdit.Text) > 0)
    )
    then 
      begin
        Result := False;
        MsgBox('TAO does not support special characters such as " ./-[]!{}\" in its home directory ' + WizardForm.DirEdit.Text, mbError, MB_OK)
      end;

  end;
end;

function CheckPortOccupied(Port:String):Boolean;
  var
    ResultCode: Integer;
  begin
   Exec(ExpandConstant('{cmd}'), '/C netstat -na | findstr'+' /C:":'+Port+' "', '',0,ewWaitUntilTerminated, ResultCode);
    if ResultCode  <> 1 then 
    begin
      Log('this port('+Port+') is occupied');
      Result := True; 
    end else
    begin
      Result := False;
    end;
  end;
